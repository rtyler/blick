buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:1.0.3'
        //classpath 'com.github.jruby-gradle:jruby-gradle-jar-plugin:1.0.2'
    }

}
apply plugin: 'com.github.jruby-gradle.jar'
import com.github.jrubygradle.JRubyExec

version = 1.0

jruby.defaultRepositories = false
jruby.defaultVersion = '1.7.19'

repositories {
    maven { url 'http://rubygems-jrubygradle.rhcloud.com/proxy/maven/releases/' }
    jcenter()
}

dependencies {
    gems group: 'rubygems', name: 'protobuf', version: '3.0.+'
    gems group: 'rubygems', name: 'concurrent-ruby', version: '0.7.+'
    gems group: 'rubygems', name: 'rake', version: '10.3.+'

    jrubyExec 'rubygems:rspec:3.3.0'
    jrubyExec 'rubygems:rspec-its:1.2.0+'
    jrubyExec 'rubygems:pry:0.10.0+'
}

configurations {
    jrubyExec.extendsFrom gems
    jrubyJar.extendsFrom gems
}

task spec(type: JRubyExec) {
    group 'JRuby'
    description 'Execute the RSpecs in JRuby'
    script 'rspec'
    inputs.source fileTree('spec').include('**/*.rb'), fileTree('lib').include('**/*.rb')
}

jrubyJar {
    initScript "${projectDir}/bin/blick-agent"
    from 'lib'
    dependsOn spec
}
assemble.dependsOn jrubyJar

// Pull the contents of lib and bin into the root of the created jar file
sourceSets {
    main {
        resources.srcDirs = ['lib', 'bin']
    }
}

// vim: ft=groovy
